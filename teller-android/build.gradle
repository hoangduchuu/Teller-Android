apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

ext {
    bintrayRepo = 'Teller-Android'
    bintrayName = 'com.levibostian.teller-android'

    publishedGroupId = 'com.levibostian.teller'
    libraryName = 'Teller-Android'
    artifact = 'teller-android'

    libraryDescription = 'Android library that makes your apps faster. Teller facilitates the downloading, saving, and reading of the cached data of your app. Keep your user\'s data fresh and remove those annoying loading screens!'

    siteUrl = 'https://levibostian.github.io/Teller-Android/'
    gitUrl = 'https://github.com/levibostian/Teller-Android.git'

    libraryVersion = '0.1.0-alpha'

    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName libraryVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled false
        }
    }
    lintOptions {
        warningsAsErrors true
    }
    sourceSets {
        // Put code into below directory that you want shared between unit and integration tests.
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "docs/javadoc"
    moduleName = 'teller'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    // Multidex support - required when minSdk < 21
    debugImplementation 'com.android.support:multidex:1.0.3'

    // JUnit
    testImplementation 'junit:junit:4.12'

    // Android Testing Support Library's runner and rules
    def androidTestLibVersion = "1.1.0"
    androidTestImplementation "androidx.test:runner:$androidTestLibVersion"
    androidTestImplementation "androidx.test:rules:$androidTestLibVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidTestLibVersion"
    androidTestImplementation "androidx.test.ext:truth:$androidTestLibVersion"

    // RxSharedPreferences
    testImplementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'
    androidTestImplementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    // Truth
    def truthVersion = "0.42"
    testImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // Mockito
    def mockitoVersion = "2.24.0"
    def mockitoKotlinVersion = "1.5.0"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    // Espresso UI Testing
    def espressoVersion = "3.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"

    // androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"

    // androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"
}
repositories {
    mavenCentral()
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

apply from: 'publish.gradle'